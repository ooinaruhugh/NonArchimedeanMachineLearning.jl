"""
Loss and optimizer state containers together with the core optimization loop
API.
"""

@doc raw"""
    Loss{F1,F2}

A batch-oriented loss function structure for optimization.

Wraps both an evaluation function and a gradient function. Both functions should
be closures that capture any necessary data (for example training data) and
operate on batches.

# Fields
- `eval::F1`: Function with signature `(params) -> values`, where `params` is a
  collection of parameter polydiscs and `values` is the corresponding collection
  of loss values
- `grad::F2`: Function with signature `(tangents) -> values`, where `tangents`
  is a collection of tangent vectors and `values` is the corresponding collection
  of directional derivatives
"""
struct Loss{F1, F2}
    eval::F1
    grad::F2
end

function Base.:+(f::Loss, g::Loss)
    eval = x -> f.eval(x) + g.eval(x)
    grad = x -> f.grad(x) + g.grad(x)
    return Loss(eval, grad)
end

@doc raw"""
    AbstractOptimConfig

Supertype of all optimizer configurations (e.g. `GreedyDescentConfig`,
`GradientDescentConfig`, `RandomDescentConfig`, `DOOConfig`, `MCTSConfig`,
`DAGMCTSConfig`).

Configurations of optimizers that descend along the children of a polydisc
provide the fields `strict::Bool`, `degree::Int` and `start_branch::Int`.
"""
abstract type AbstractOptimConfig end

# Children of `param` considered in one descent step, together with the branch
# to descend along in the next step.  In strict mode, only the coordinate
# `next_branch` is refined and the branches are cycled through; otherwise all
# children of the given degree are returned.
function _descent_candidates(
        param::ValuationPolydisc,
        next_branch::Int,
        config::AbstractOptimConfig
)
    if config.strict
        below_nodes = children_along_branch(param, next_branch)
        next_branch = next_branch == dim(param) ? 1 : next_branch + 1
    else
        below_nodes = children(param, config.degree)
    end
    return below_nodes, next_branch
end

# TODO: possible refactor:
# We can bundle the value of param in the state, 
# by assuming that the state type always has a method
# "get_param!" available.  

@doc raw"""
    OptimSetup{S,T,N,U,V,L,O}

Complete optimization setup containing loss, parameters, optimizer, and state.

Mutable structure that captures everything needed for optimization. The loss function
should have data baked in as a closure.

# Fields
- `loss::L`: Loss function (closure over data) with a batch evaluation method and
  a batch directional-derivative method
- `param::ValuationPolydisc{S,T,N}`: Current parameter values (mutable during optimization)
- `optimiser::O`: Optimizer function
  `(loss, param, state, context) -> (new_param, new_state, converged)`
- `state::U`: Optimization state (e.g., previous steps, momentum, etc.)
- `context::V`: Optimizer settings (e.g., learning rate, degree, etc.)
- `converged::Bool`: Whether the optimizer has converged

# Type Parameters
- `S`: Coefficient type (typically p-adic numbers)
- `T`: Radius/valuation type
- `N`: Dimension of parameter space
- `U`: State type
- `V`: Context type
- `L`: Concrete loss type
- `O`: Concrete optimizer callable type
"""
mutable struct OptimSetup{S, T, N, U, V, L <: Loss, O}
    # The loss function (should be a closure over any data)
    # loss.eval should have type (param) -> scalar
    # loss.grad should have type (tangent_vector) -> scalar
    loss::L
    # The current parameter value
    param::ValuationPolydisc{S, T, N}
    # An optimiser is a function that takes in the loss and param
    # (plus eventually other parameters, e.g. learning rate)
    # and outputs a new choice of parameters
    optimiser::O
    # The state is an optional field that records the state of the optimisation
    # process, e.g. previous steps that were made, etc.
    # This is useful since some optimisation methods may depend on the state.
    state::U
    # The context type. This records things like settings for the optimiser, etc
    context::V
    # Whether the optimisation has converged (e.g. no children remain)
    converged::Bool
end

@doc raw"""
    eval_loss(optim::OptimSetup)

Evaluate the loss function at the current parameter values.

# Arguments
- `optim::OptimSetup`: The optimization setup

# Returns
Scalar value of the loss at the current parameters
"""
function eval_loss(optim::OptimSetup)
    # @show methods(optim.loss.eval)
    # @show typeof([optim.param])
    return optim.loss.eval([optim.param])[1]
end

@doc raw"""
    update_param!(optim::OptimSetup{S,T,N,U,V,L,O}, param::ValuationPolydisc{S,T,N}) where {S,T,N,U,V,L,O}

Update the parameter values in the optimization setup.

# Arguments
- `optim::OptimSetup{S,T,N,U,V,L,O}`: The optimization setup
- `param::ValuationPolydisc{S,T,N}`: New parameter values

# Notes
Mutates the optimization setup in place.
"""
function update_param!(
        optim::OptimSetup{S, T, N, U, V, L, O},
        param::ValuationPolydisc{S, T, N}
) where {S, T, N, U, V, L, O}
    optim.param = param
end

@doc raw"""
    update_state!(optim::OptimSetup{S,T,N,U,V,L,O}, state::U) where {S,T,N,U,V,L,O}

Update the optimizer state in the optimization setup.

# Arguments
- `optim::OptimSetup{S,T,N,U,V,L,O}`: The optimization setup
- `state::U`: New state value

# Notes
Mutates the optimization setup in place.
"""
function update_state!(optim::OptimSetup{S, T, N, U, V, L, O}, state::U) where {
        S, T, N, U, V, L, O}
    optim.state = state
end

@doc raw"""
    step!(optim_setup::OptimSetup)

Perform one optimization step.

Calls the optimizer function to compute new parameters, state, and convergence
status, then updates the optimization setup accordingly.

# Arguments
- `optim_setup::OptimSetup`: The optimization setup

# Notes
Mutates the optimization setup by updating both parameters and state.
"""
function step!(optim_setup::OptimSetup)
    new_param, new_state,
    converged = optim_setup.optimiser(
        optim_setup.loss,
        optim_setup.param,
        optim_setup.state,
        optim_setup.context
    )
    update_param!(optim_setup, new_param)
    update_state!(optim_setup, new_state)
    optim_setup.converged = converged
    return converged
end

@doc raw"""
    has_converged(optim::OptimSetup) -> Bool

Check whether the optimization has converged.

Convergence is detected when the optimizer can no longer refine parameters,
typically because the polydisc radius has reached the precision of the p-adic field.

# Arguments
- `optim::OptimSetup`: The optimization setup

# Returns
`true` if the optimization has converged, `false` otherwise.
"""
function has_converged(optim::OptimSetup)
    return optim.converged
end

@doc raw"""
    optimize!(optim::OptimSetup, max_steps::Int; verbose::Bool=false) -> Int

Run optimization until convergence or `max_steps`, whichever comes first.

# Arguments
- `optim::OptimSetup`: The optimization setup
- `max_steps::Int`: Maximum number of steps to take
- `verbose::Bool=false`: If `true`, print loss at each step

# Returns
The number of steps taken. Check `has_converged(optim)` to distinguish
early convergence from hitting `max_steps`.

# Example
```julia
optim = greedy_descent_init(param, loss, GreedyDescentConfig(strict = false, degree = 1))
steps = optimize!(optim, 100; verbose=true)
if has_converged(optim)
    println("Converged after \$steps steps")
else
    println("Reached max steps (\$steps)")
end
```
"""
function optimize!(optim::OptimSetup, max_steps::Int; verbose::Bool = false)
    for i in 1:max_steps
        converged = step!(optim)
        if verbose
            @printf("Step %d: loss = %.6e%s\n", i, eval_loss(optim),
                converged ? " [converged]" : "")
        end
        if converged
            return i
        end
    end
    return max_steps
end
